#!/usr/bin/env zsh

local _TERM_BACKUP="${TERM}"

# Update and upgrade stuff
() {
  TERM="xterm-256color"

  # Style
  local reset="$(tput sgr0)"
  local bold="$(tput bold)"
  local icon="➥"
  local cmd_style="$(tput setaf 102)"
  local log_style="$(tput setaf 59)"
  # Colors
  local blue="$(tput setaf 4)"
  local magenta="$(tput setaf 5)"
  local red="$(tput setaf 1)"
  local yellow="$(tput setaf 3)"

  local verbose=""

  if [[ "$1" == "-v" || "$1" == "--verbose" ]] ; then
    verbose='--verbose'
  fi

  function bubu::timestamp {
    date '+%b %d @ %I:%M:%S %p'
  }

  function bubu::banner {
    local color="$1"
    local msg="$2"
    local ico="${3:-$icon}"

    printf "%s %s %s \n" \
      "${color}${bold}${ico}${reset}" \
      "${color}${msg}${reset}" \
      "${cmd_style}$(bubu::timestamp)${reset}"
  }

  function bubu::style_cmd {
    printf "%s  $ %s %s \n" \
      "$cmd_style" \
      "$1" \
      "$reset"
  }

  function bubu::style_output {
    # Pipe command output to this function for formatted output.
    #
    # For example:
    #     echo 'Hello world :)' | bubu::style_output
    # output:
    #     >>  | Hello world :)
    sed 's/^\(.*\)$/'"${log_style}  | ${reset}"'\1/g'
  }

  function bubu::cmd {
    bubu::style_cmd "$1"
    eval "$1" 2>&1 | bubu::style_output
    echo

    # TODO: Use `script` to capture colored output
    # https://stackoverflow.com/questions/3515208/can-colorized-output-be-captured-via-shell-redirect
    #   local args=( "$@" )
    #   bubu::style_cmd "${args}"
    #   script -q /dev/null --command "${args}" < /dev/null | bubu::style_output
  }

  # Homebrew
  bubu::banner "$magenta" "Update brew"
  bubu::cmd "brew update ${verbose}"
  bubu::banner "$blue" "Upgrade brew"
  bubu::cmd "brew upgrade --display-times"
  echo

  # oh-my-zsh
  bubu::banner "$blue" "Upgrade oh-my-zsh"
  bubu::cmd "( cd ~/.oh-my-zsh && git pull --rebase --stat origin master )"
  local LAST_EPOCH=$(( $EPOCHSECONDS / 60 / 60 / 24 ))
  bubu::style_cmd "echo \"LAST_EPOCH=${LAST_EPOCH}\" >! ${ZSH_CACHE_DIR//${HOME}/~}/.zsh-update"
  echo "LAST_EPOCH=${LAST_EPOCH}" > "${ZSH_CACHE_DIR}/.zsh-update"
  echo

  TERM="$_TERM_BACKUP"
  unset _TERM_BACKUP

  bubu::banner "$red" "${yellow}Done" "☭"
} "$@"


# vim: set ft=zsh:
